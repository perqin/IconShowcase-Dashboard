/*
 * Copyright (c) 2017 Jahir Fiquitiva
 *
 * Licensed under the CreativeCommons Attribution-ShareAlike
 * 4.0 International License. You may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *    http://creativecommons.org/licenses/by-sa/4.0/legalcode
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Special thanks to the project contributors and collaborators
 * 	https://github.com/jahirfiquitiva/IconShowcase#special-thanks
 */

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

ext {
    TARGET_SDK = 26
    BUILD_TOOLS = "27.0.3"
    ANDROID_SUPPORT_LIBS = "27.0.2"
}

android {
    compileSdkVersion TARGET_SDK
    buildToolsVersion BUILD_TOOLS

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion TARGET_SDK
        versionCode 1980
        versionName "1.9.8.0"
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'proguard-rules.pro'
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res/drawable',
                    'src/main/res/drawable/dashboard',
                    'src/main/res/drawable/designer',
                    'src/main/res/drawable/icon_pack',
                    'src/main/res/drawable/launchers_icons',
                    'src/main/res/drawable/splash_screens',
                    'src/main/res/drawable/widgets',
                    'src/main/res/layout',
                    'src/main/res/layout/activities',
                    'src/main/res/layout/extras',
                    'src/main/res/layout/items',
                    'src/main/res/layout/sections',
                    'src/main/res/layout/widgets',
                    'src/main/res/values',
                    'src/main/res/values/private-values',
                    'src/main/res/values/icon_pack',
                    'src/main/res/values/translations'
            ]
        }
    }

    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
        }
        aaptOptions {
            noCompress 'zip', 'komp', 'klwp', 'kwgt', 'klck', 'kwch'
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //Android Support Libraries
    //noinspection GradleCompatible
    compile "com.android.support:design:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:palette-v7:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:gridlayout-v7:$ANDROID_SUPPORT_LIBS"
    compile "com.android.support:customtabs:$ANDROID_SUPPORT_LIBS"

    //OkHttp
    compile 'com.squareup.okhttp3:okhttp:3.8.0'

    //Glide Library - Image Loading Lib
    compile 'com.github.bumptech.glide:glide:3.7.0'

    //RecyclerView Fast Scroller
    compile 'com.github.pluscubed:recycler-fast-scroll:ab973f2'

    //Sectioned RecyclerView
    compile 'com.afollestad:sectioned-recyclerview:0.4.0'

    //Circular Image View
    compile 'com.github.hdodenhof:CircleImageView:7fd3573'

    //Library for Requests
    compile 'com.github.AllanWang:Icon-Request-Tool:a87db9d'

    //Library for UI framework
    compile 'com.github.AllanWang:Capsule:f7a906e'

    //Donations Lib
    compile 'org.sufficientlysecure:donations:2.5'

    //Muzei API
    //noinspection GradleDynamicVersion
    compile 'com.google.android.apps.muzei:muzei-api:2.0'

    //License Checker Lib
    compile 'com.github.javiersantos:PiracyChecker:1.1'

    //Required for Kustom support.
    //noinspection GradleDynamicVersion
    compile 'org.bitbucket.frankmonza:kustomapi:+'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}
